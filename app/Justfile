set dotenv-load := true
export PATH := "./node_modules/.bin:" + env_var('PATH')
export SERVICE_NAME := "tsdoc.static"

help:
    @just --list --unsorted

install:
    pnpm install
alias i := install

bootstrap:
    git submodule update --init --recursive
    pnpm install

check:
    tsc --noEmit
    eslint src
alias c := check

lint:
    eslint src

fix:
    eslint src --fix

test FILTER="":
    vitest run {{FILTER}}
alias t := test

run:
    vite
alias r := run

clean:
    rm -rf build/
    rm -rf stage/
    mkdir stage/

stage:
    node -r esbuild-register script/stage-package.ts

lib:
    #!/bin/bash -e
    function show() { dye -m -- "$@"; "$@"; }

    export BUILD_PACKAGE_NAME=$(ls stage | rg -v \\.)
    export BUILD_PACKAGE_VERSION=$(cat stage/$BUILD_PACKAGE_NAME/package.json | jq -r .version)

    show modenv run -p -- vite build --base /$BUILD_PACKAGE_NAME/$BUILD_PACKAGE_VERSION/ --outDir library/$BUILD_PACKAGE_NAME/$BUILD_PACKAGE_VERSION/
    show vite build --outDir library/$BUILD_PACKAGE_NAME/$BUILD_PACKAGE_VERSION/server --ssr src/libdoc/ssr.tsx
    show node -r esbuild-register script/generate-lib-docs.ts $BUILD_PACKAGE_NAME $BUILD_PACKAGE_VERSION stage/static_urls.txt
    show rm -rf library/$BUILD_PACKAGE_NAME/$BUILD_PACKAGE_VERSION/server

link:
    fd . library/ -d 1 -x cp -r {} build/{/}

unlink:
    fd . library/ -d 1 -x rm -r build/{/}

run-static:
    cd build/static && http

watch:
    vite build --watch --outDir build/client

release:
    #rm -rf stage
    NODE_ENV= pnpm install
    vite build --outDir build
    vite build --outDir build/server --ssr src/tsdoc/ssr.tsx
    node -r esbuild-register script/generate-static.ts
    rm -rf build/server
    just link
    fd . build

ios:
    open ./ios/*.xcodeproj/project.xcworkspace

release-ios:
    cd ios && modenv run -p xcodebuild
    open ios/build/Release-iphoneos/
    @echo $(dye -c INFO) Drag the .app to the device in Xcode Device Manager

run-release:
    vite preview

deploy:
    render put-env $SERVICE_NAME .env.production
    render deploy $SERVICE_NAME
